buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'com.github.spotbugs' version '1.6.0'
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'ch.hsr.adv'
version = '0.1'

/****************************************
 * instructions for all projects
 ****************************************/
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'com.bmuschko.nexus'

    repositories {
        jcenter()
        mavenCentral()
    }

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
}

/****************************************
 * instructions for each sub project
 ****************************************/
subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    checkstyle {
        ignoreFailures = false
        showViolations = true
        configFile = file("${project.rootDir}/checkstyle.xml")
        checkstyleTest.enabled = false
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories = files(sourceSets.main.output)
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    spotbugs {
        excludeFilter = file("$project.rootDir/excludeFilter.xml")
    }

    // To generate an HTML report instead of XML
    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    // common dependencies
    dependencies {
        //only use slf4j implementation (e.g. logback) in core
        compile "org.slf4j:slf4j-api:1+"
        compile group: 'com.google.code.gson', name: 'gson', version: '2+'

        testCompile "junit:junit:4+"
        testCompile 'org.jukito:jukito:1+'
    }
}

/****************************************
 * Single library jar containing all sub projects and 3rd party dependencies
 ****************************************/
configurations {
    childJars
}

dependencies {
    subprojects.each {
        childJars project(it.path)
    }
}

jar {
    dependsOn configurations.childJars
    exclude('**/logback.xml')
    baseName project.name
    version project.version
    from { configurations.childJars.collect { zipTree(it) } }
}


/****************************************
 * Test coverage upload
 ****************************************/
task jacocoRootReport(type: JacocoReport) {

    dependsOn = subprojects.test

    description = 'Generates an aggregate report from all subprojects'

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }

    onlyIf = {
        true
    }
}

check.finalizedBy(jacocoRootReport)

/****************************************
 * Bintray deployment (needed for Maven Central)
 ****************************************/
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MavenCustom']
    pkg {
        repo = 'adv'
        name = 'adv'
        userOrg = 'adv'
        licenses = ['AGPL-V3']
        vcsUrl = 'https://github.com/ADVisualizer/ADV-Commons.git'
    }
    version {
        name = project.version
        released = new Date()
    }
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name 'GNU AFFERO GENERAL PUBLIC LICENSE, Version 3.0'
            url 'https://www.gnu.org/licenses/agpl.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'mwieland'
            name 'Michael Wieland'
            email 'mwieland@hsr.ch'
        }
        developer {
            id 'mtrentini'
            name 'Muriele Trentini'
            email 'mtrentin@hsr.ch'
        }
    }

    scm {
        url 'https://github.com/ADVisualizer/ADV-UI'
        connection 'https://git@github.com/ADVisualizer/ADV-UI.git'
        developerConnection 'scm:git:https://github.com/ADVisualizer/ADV-UI.git'
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'ch.hsr.adv'
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Common classes of the algorithm and data structure visualizer project')
                root.appendNode('name', 'ADV Commons')
                root.appendNode('url', 'https://github.com/ADVisualizer/ADV-Commons')
                root.children().last() + pomConfig
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenCustomPublication {
        destination = file("$buildDir/libs/pom.xml")
    }
}



